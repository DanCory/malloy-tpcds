import "tpcds.malloy"

source: catalog_year_total is from(
  catalog_sales -> {
    group_by:
      customer.c_customer_id
      customer.c_first_name
      customer.c_last_name
      customer.c_preferred_cust_flag

    aggregate:
      total_sales_2001 is total_sales { where: date_dim.d_year = 2001 }
      total_sales_2002 is total_sales { where: date_dim.d_year = 2002 }
  }
) {}

source: web_year_total is from(
  web_sales -> {
    group_by:
      customer.c_customer_id
      customer.c_first_name
      customer.c_last_name
      customer.c_preferred_cust_flag

    aggregate:
      total_sales_2001 is total_sales { where: date_dim.d_year = 2001 }
      total_sales_2002 is total_sales { where: date_dim.d_year = 2002 }
  }
) {}

source: year_total is from(
  store_sales -> {
    group_by:
      customer.c_customer_id
      customer.c_first_name
      customer.c_last_name
      customer.c_preferred_cust_flag

    aggregate:
      total_sales_2001 is total_sales { where: date_dim.d_year = 2001 }
      total_sales_2002 is total_sales { where: date_dim.d_year = 2002 }
  }
) {
  join_one: web_year_total on c_customer_id = web_year_total.c_customer_id
  join_one: catalog_year_total on c_customer_id = catalog_year_total.c_customer_id
  join_one: customer on c_customer_id = customer.c_customer_id

  dimension: store_yoy is total_sales_2002 / total_sales_2001
  dimension: catalog_yoy is catalog_year_total.total_sales_2002 / catalog_year_total.total_sales_2001
  dimension: web_yoy is web_year_total.total_sales_2002 / web_year_total.total_sales_2001
}

query: year_total -> {
  project:
    c_customer_id
    c_first_name
    c_last_name
    c_preferred_cust_flag

  where:
    total_sales_2001 > 0
    and web_year_total.total_sales_2001 > 0
    and catalog_year_total.total_sales_2001 > 0
    and total_sales_2002 > 0
    and web_year_total.total_sales_2002 > 0
    and catalog_year_total.total_sales_2002 > 0
    and catalog_yoy > store_yoy
    and catalog_yoy > web_yoy

  order_by:
    c_customer_id 
    c_first_name 
    c_last_name 
    c_preferred_cust_flag 
}
